grammar org.apache.pigeditor.Pig with org.eclipse.xtext.common.Terminals

generate pig "http://www.apache.org/pigeditor/Pig"

/*
 * Grammar heavily borrowed from Pig and adapted to Xtext. 
 * 
 * There are a few conflicts to solve.
 */
 
Query :
    (elements+=Statement)*;

Statement :
    (DefineStatement |
      LoadStatement |
	  (ForeachStatement |
	    FilterStatement |
	    GroupByStatement |
	    OrderStatement |
	    LimitStatement |
	    DistinctStatement |
	    CrossStatement |
	    JoinStatement |
	    CoGroupByStatement |
	    StreamStatement ) ParallelClause? |
       MrStatement |
       SplitStatement |
       UnionStatement |
       SampleStatement |
       StoreStatement |
       DumpStatement |
       DescribeStatement |
       ExplainStatement) ';' |
     DeclareClause ';'? |
     CommentStatement 
;


DefineStatement :
    RegisterClause | 
    DefineClause |
    DefaultClause
;

LoadStatement :
    name=Alias '=' 'LOAD' STRING 
                  ('USING' ('JsonLoader' |
							'PigStorage' |
							'TextLoader'  | 
							'org.apache.pig.piggybank.storage.CSVLoader' |
							'org.apache.pig.piggybank.storage.HadoopJobHistoryLoader' |
							'org.apache.pig.piggybank.storage.MyRegExLoader' |
							'org.apache.pig.piggybank.storage.PigStorageSchema' |
							'org.apache.pig.piggybank.storage.RegExLoader' |
							'org.apache.pig.piggybank.storage.SequenceFileLoader' |
							'org.apache.pig.piggybank.storage.XMLLoader' |
							'org.apache.pig.piggybank.storage.apachelog.CombinedLogLoader' |
							'org.apache.pig.piggybank.storage.apachelog.CommonLogLoader' | FuncName) '(' STRING? (',' STRING)* ')')?
                   AsClause?
;

ForeachStatement :
    name=Alias '=' 'FOREACH' type=AliasType 
                    (items+=nested_blk ';'? | items+=generate_clause) 
;

FilterStatement :
    name=Alias '=' 'FILTER' type=AliasType 
                   'BY' cond=Condition 
;

GroupByStatement :
    name=Alias '=' 'GROUP' type=AliasType 
                   items=group_item_list ( 'USING' group_type )? partition_clause? 
;

CoGroupByStatement :
    name=Alias '=' 'COGROUP' aliases=join_sub_clause ( 'USING' COGROUP_TYPE )? partition_clause? 
;

terminal COGROUP_TYPE :
    "'" ( ('merge') ) "'"
;

OrderStatement : 
    name=Alias '=' 'ORDER' type=AliasType  'BY' order_by_clause
;

CrossStatement : 
    name=Alias '=' 'CROSS' aliases=rel_list partition_clause?
;

UnionStatement : 
    name=Alias '=' 'UNION' 'ON' aliases=rel_list
;

JoinStatement : 
     name=Alias '=' 'JOIN' aliases=join_sub_clause ( 'USING' join_type )? partition_clause?
;

LimitStatement : 
    name=Alias '=' 'LIMIT' type=AliasType scalarInt
;

SampleStatement : 
    name=Alias '=' 'SAMPLE' type=AliasType DOUBLENUMBER
;

DistinctStatement : 
    name=Alias '=' 'DISTINCT' type=AliasType partition_clause?
;

StreamStatement : 
    name=Alias '=' 'STREAM' type=AliasType 'THROUGH' ( EXECCOMMAND | IDENTIFIER ) AsClause?
;

MrStatement : 
    name=Alias '=' 'MAPREDUCE' STRING store=StoreStatement load=LoadStatement EXECCOMMAND?
;

SplitStatement : 
    name=Alias '=' 'SPLIT' type=AliasType  'INTO' branches+=split_branch ( ',' branches+=split_branch )+
;

StoreStatement :
    'STORE' type=AliasType 'INTO' STRING ( 'USING' FuncClause )?
;

DescribeStatement : 
    'DESCRIBE' type=AliasType
;

ExplainStatement : 
    'EXPLAIN' type=AliasType
;

DumpStatement : 
    'DUMP' type=AliasType
;



Condition returns Condition: 
    ( '(' Type ')' )? OrCondition
;

OrCondition returns Condition: 
    AndCondition ({OrCondition.left=current} 'OR' right=AndCondition )*
;

AndCondition returns Condition: 
    UnaryCondition ({AndCondition.left=current} 'AND' right=UnaryCondition )*
;

UnaryCondition returns Condition: 
    // - / % + 0.0
    '(' condition=Condition ('?' leftExp=Condition ':' rightExp=Condition)? ')' ({UnaryCondition.left=current} '+' right=Condition)? |    
    //null_check_cond | // to LL refactor
    TE ({UnaryCondition.left=current} rel_op right=TE)? 
;

TE returns Condition:
    Expression ('IS' 'NOT'? 'NULL')?
;

func_eval: 
   ('ABS' |
	'ACOS' |
	'ARITY' |
	'ASIN' |
	'ATAN' |
	'AVG' |
	'Bloom' |
	'CBRT' |
	'CEIL' |
	'CONCAT' |
	'COR' |
	'COS' |
	'COSH' |
	'COUNT' |
	'COUNT_STAR' |
	'COV' |
	'DIFF' |
	'EXP' |
	'FLOOR' |
	'INDEXOF' |
	'InvokeForDouble' |
	'InvokeForFloat' |
	'InvokeForInt' |
	'InvokeForLong' |
	'InvokeForString' |
	'IsEmpty' |
	'LAST_INDEX_OF' |
	'LCFIRST' |
	'LOG' |
	'LOG10' |
	'LOWER' |
	'MAX' |
	'MIN' |
	'RANDOM' |
	'REGEX_EXTRACT' |
	'REGEX_EXTRACT_ALL' |
	'REPLACE' |
	'ROUND' |
	'SIN' |
	'SINH' |
	'SIZE' |
	'SQRT' |
	'STRSPLIT' |
	'SUBSTRING' |
	'SUM' |
	'TAN' |
	'TANH' |
	'TOBAG' |
	'TOKENIZE' |
	'TOMAP' |
	'TOP' |
	'TOTUPLE' |
	'TRIM' |
	'UCFIRST' |
	'UPPER' |
	'org.apache.pig.piggybank.evaluation.ExtremalTupleByNthField' |
	'org.apache.pig.piggybank.evaluation.MaxTupleBy1stField' |
	'org.apache.pig.piggybank.evaluation.datetime.DiffDate' |
	'org.apache.pig.piggybank.evaluation.datetime.convert.CustomFormatToISO' |
	'org.apache.pig.piggybank.evaluation.datetime.convert.ISOToUnix' |
	'org.apache.pig.piggybank.evaluation.datetime.convert.UnixToISO' |
	'org.apache.pig.piggybank.evaluation.datetime.diff.ISODaysBetween' |
	'org.apache.pig.piggybank.evaluation.datetime.diff.ISOHoursBetween' |
	'org.apache.pig.piggybank.evaluation.datetime.diff.ISOMinutesBetween' |
	'org.apache.pig.piggybank.evaluation.datetime.diff.ISOMonthsBetween' |
	'org.apache.pig.piggybank.evaluation.datetime.diff.ISOSecondsBetween' |
	'org.apache.pig.piggybank.evaluation.datetime.diff.ISOYearsBetween' |
	'org.apache.pig.piggybank.evaluation.datetime.truncate.ISOToDay' |
	'org.apache.pig.piggybank.evaluation.datetime.truncate.ISOToHour' |
	'org.apache.pig.piggybank.evaluation.datetime.truncate.ISOToMinute' |
	'org.apache.pig.piggybank.evaluation.datetime.truncate.ISOToMonth' |
	'org.apache.pig.piggybank.evaluation.datetime.truncate.ISOToSecond' |
	'org.apache.pig.piggybank.evaluation.datetime.truncate.ISOToWeek' |
	'org.apache.pig.piggybank.evaluation.datetime.truncate.ISOToYear' |
	'org.apache.pig.piggybank.evaluation.util.apachelogparser.DateExtractor' |
	'org.apache.pig.piggybank.evaluation.util.apachelogparser.HostExtractor' |
	'org.apache.pig.piggybank.evaluation.util.apachelogparser.SearchEngineExtractor' |
	'org.apache.pig.piggybank.evaluation.util.apachelogparser.SearchTermExtractor' | FuncName) {func_name} '(' arg_list=real_arg_list? ')'
;

real_arg_list : 
    args+=real_arg ( ',' args+=real_arg )* 
;

real_arg : 
    condition=Condition | condition_star=STAR
;

Expression returns Expression: 
    'NOT'? AddExpression
;

AddExpression returns Expression: 
    multi_expr ( ( PLUS | MINUS ) mul_exp+=multi_expr )*
;

multi_expr returns Expression: 
    cast_expr ( ( STAR | DIV | PERCENT ) mul_cast+=cast_expr )*
;

cast_expr returns Expression:
    unary_expr    
;

unary_expr returns Expression:  
     expr_eval | neg_expr
;

expr_eval returns Expression: 
    const_expr {const_expr} | var_expr
;

var_expr : 
    projectable_expr ( dot_proj | pound_proj )*
;

projectable_expr : 
   func_eval | col_ref {projectable_expr}
;

dot_proj : 
    ('.' | '::') (  col_alias_or_index )
;

col_alias_or_index : 
    col_alias | col_index
;

col_alias : 
    IDENTIFIER
;

col_index : DOLLAR (DIGIT | INT)
;

pound_proj : POUND ( STRING | 'NULL' )
;

bin_expr: 
    '(' condition=Condition '?' leftExp=Expression ':' rightExp=Expression ')'
;

neg_expr returns Expression : 
    '-' cast_expr
;

rel_op :   
rel_op_eq
       | rel_op_ne
       | rel_op_gt
       | rel_op_gte
       | rel_op_lt
       | rel_op_lte
       | 'MATCHES'
;

rel_op_eq : STR_OP_EQ | NUM_OP_EQ
;

rel_op_ne : STR_OP_NE | NUM_OP_NE
;

rel_op_gt : STR_OP_GT | NUM_OP_GT
;

rel_op_gte : STR_OP_GTE | NUM_OP_GTE
;

rel_op_lt : STR_OP_LT | NUM_OP_LT
;

rel_op_lte : STR_OP_LTE | NUM_OP_LTE
;

AliasType :
    referenced=[Statement]
;

FlattenGeneratedItem : 
   (Condition | '*') ( 'AS' ( FieldDef | FieldFefList ) )?
;

CommentStatement :
    SL_COMMENT
;

Alias : 
    ID
;

ParallelClause : 
     'PARALLEL' scalarInt;


RegisterClause : 
    'REGISTER' PathClause
;

DefaultClause : 
    '%DEFAULT' name=Alias STRING
;

DeclareClause : 
    '%DECLARE' name=Alias (STRING | EXECCOMMAND)
;

DefineClause : 
    'DEFINE' name=Alias FuncClause
;

FuncClause : 
   FuncName '(' STRING? (',' STRING)* ')'
;

FuncName :
     (ID | '.' | '$')+ 
;

PathClause :
    ('ALL' | scalar | ID | '.' | '$' | '/' | '\\' | '-')+
;

AsClause:
    'AS' (AliasDef | AliasDefList)
;

AliasDef:
    Alias (':' Type)?
;

AliasDefList:
    '(' AliasDef ( ',' AliasDef )* ')'
;     

IDENTIFIER: 
    ID //LETTER ( DIGIT | LETTER | SPECIALCHAR )*
;

FieldDef :
    IDENTIFIER ( ':' Type )?
;

FieldFefList : 
    '(' FieldDef ( ',' FieldDef )* ')'
;    

Type : 
    SimpleType | TupleType | MapType | bag_type 
;

SimpleType : 
    'INT' |
    'LONG' | 
    'FLOAT' | 
    'DOUBLE' | 
    'CHARARRAY' | 
    'BYTEARRAY'
;

TupleType : 
    'TUPLE'? AliasDefList
;

bag_type : 
    'BAG'? '{' ( ( IDENTIFIER ':' )? TupleType | MapType )? '}'
;

MapType : 
    'MAP[]'
;

Cmd : 
    EXECCOMMAND //( ship_clause | cache_caluse | input_clause | output_clause | error_clause )*
;

terminal SL_COMMENT  : '--' ! ('\n' | '\r')* ('\r'? '\n')?;


STR_OP_EQ : 'EQ'
;

STR_OP_GT : 'GT'
;

STR_OP_LT : 'LT'
;

STR_OP_GTE : 'GTE'
;

STR_OP_LTE : 'LTE'
;

STR_OP_NE : 'NEQ'
;

STR_OP_MATCHES : 'MATCHES'
;
    
NUM_OP_EQ : '=='
;

NUM_OP_LT : '<'
;

NUM_OP_LTE : '<='
;

NUM_OP_GT : '>'
;

NUM_OP_GTE : '>=' 
;

NUM_OP_NE : '!='
;


SEMI_COLON : ';'
;
    
LEFT_PAREN : '('
;
    
RIGHT_PAREN : ')'
;

LEFT_CURLY : '{'
;

RIGHT_CURLY : '}'
;

LEFT_BRACKET : '['
;
    
RIGHT_BRACKET : ']'
;

POUND : '#'
;

EQUAL : '=';

COMMA : ','
;

PERIOD : '.'
;

DIV : '/'
;

PERCENT : '%'
;

PLUS : '+'
;

MINUS : '-'
;
    
terminal DIGIT : '0'..'9'
;

terminal  LETTER : 'A'..'Z'
;
    
terminal  SPECIALCHAR : '_'
;

terminal FLOATINGPOINT : 
    DIGIT+ '.' DIGIT+
;
    
terminal LONGINTEGER: 
    INT ( 'L' )//? INT with > N digits
;
    
terminal DOUBLENUMBER : 
    FLOATINGPOINT ( 'E' ( '-' | '+' )? INT )?
;

terminal EXECCOMMAND : '`' ( !( '`' ) )* '`';

    
STAR : 
    '*'
;

COLON : 
    ':'
;

DOLLAR : 
    '$'
;

group_type :
    STRING
;

group_item_list : 
    group_item ( COMMA items+=group_item )*
;

order_by_clause : 
    STAR ( 'ASC' | 'DESC' )? |
    order_col_list
;

order_col_list : 
    order_col ( ',' order_col )*
;

order_col : 
     col_ref ( 'ASC' | 'DESC' )? | 
     '(' col_ref ( 'ASC' | 'DESC' )? ')'
;

partition_clause : 
    'PARTITION' 'BY' FuncName
;

rel_list : 
    type+=AliasType ( ',' type+=AliasType )*
;

join_type :
    STRING
;

join_sub_clause : 
    join_item ( 'LEFT' | 'RIGHT' | 'FULL' )? 'OUTER'? ( ',' others+=join_item )+
;

join_item : 
    type=AliasType expressions=join_group_by_clause    
;

group_item : 
    ('BY' join_group_by_expr_list | 'ALL' | 'ANY' ) ( 'INNER' | 'OUTER' )? 
;

join_group_by_clause : 
    'BY' join_group_by_expr_list
;

join_group_by_expr_list : 
       join_group_by_expr |
      '(' left=join_group_by_expr ( ',' expressions+=join_group_by_expr )* ')'
;

join_group_by_expr : 
    Expression | STAR {join_group_by_expr}
;

nested_blk : 
    '{' nested_command_list clauses=generate_clause ';' '}'
;

generate_clause : 
    'GENERATE' items+=FlattenGeneratedItem ( ',' items+=FlattenGeneratedItem )* 
;

nested_command_list : 
    {nested_command_list} (cmds+=nested_command SEMI_COLON)* 
;

nested_command : 
    Alias '=' nested_op
;

nested_op : // TODO some warning comes from here          
          NestedFilter | 
          nested_sort | 
          nested_distinct | 
          nested_limit | 
          ( '(' Type ')' )? AddExpression
;

NestedFilter : 
    'FILTER' exp=var_expr 'BY' cond=Condition
;

nested_sort : 
    'ORDER' var_expr 'BY' order_by_clause
;

nested_distinct : 
    'DISTINCT' var_expr
;

nested_limit : 
    'LIMIT' var_expr (INT | LONGINTEGER)
;

split_branch : 
    IDENTIFIER 'IF' Condition
;

col_ref : 
    alias_col_ref | dollar_col_ref
;

alias_col_ref : 
    'GROUP' | IDENTIFIER
;

dollar_col_ref : 
    DOLLAR INT
;

const_expr : 
    literal
;

literal : 
    scalar | STRING | '\\u' | 'NULL' //| map | bag | tuple // const type?
;

scalar : 
    FLOATINGPOINT | scalarInt //INTEGER | LONGINTEGER | FLOATNUMBER | DOUBLENUMBER | MYQUOTEDSTRING | NULL
;

scalarInt : 
    DIGIT | INT | LONGINTEGER | ('$' ID) // TODO warning
;
